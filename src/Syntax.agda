open import Agda.Primitive
open import Relation.Binary.PropositionalEquality
open import Relation.Unary hiding (_âˆˆ_)
open import Relation.Binary
open import Data.Product using (_Ã—_; _,_)
open import Function using (_âˆ˜_)

{-

   A formalization of (raw) syntax with higher-rank binders.

   We define a notion of syntax which allows for higher-rank binders, variables and substitutions. Ordinary notions of
   variables are special cases:

   * rank 1: ordinary variables and substitutions, for example those of Î»-calculus
   * rank 2: meta-variables and their instantiations
   * rank 3: symbols (term formers) in dependent type theory, such as Î , Î£, W, and syntactic transformations between theories

   This version has a single notion of syntactic entity. A generalization would have an additional parameter
   specifying a set of type classes.

-}


module Syntax where

  infixl 5 _âŠ•_

  {- Shapes are a kind of variable contexts. They assign to each variable its syntactic arity, which is a binding shape.
     We model shapes as binary trees so that it is easy to concatenate two of them. A more traditional approach models
     shapes as lists, in which case one has to append lists. -}

  -- We refer to a Shape when we have in mind an entity that describes available variables,
  -- and to an Arity when we think of the description of the arguments that a variable/symbol accepts.
  -- Formally, they are the same thing, though.

  data Shape : Set

  Arity : Set
  Arity = Shape

  arg : Arity â†’ Shape
  arg Î³ = Î³

  data Shape where
    ğŸ˜ : Shape -- the empty shape
    [_] : Arity â†’ Shape -- the shape with precisely one variable
    _âŠ•_ : âˆ€ (Î³ : Shape) (Î´ : Shape) â†’ Shape -- disjoint sum of shapes

  -- -- Examples:

  ordinary-variable-arity : Shape
  ordinary-variable-arity = ğŸ˜

  binary-type-metavariable-arity : Shape
  binary-type-metavariable-arity = [ [ ğŸ˜ ] âŠ• [ ğŸ˜ ] ]

  Î -arity : Shape
  Î -arity = [ [ ğŸ˜ ] âŠ• [ [ ğŸ˜ ] ] ]

  infix 3 _âˆˆ_

  {- The de Bruijn indices are binary numbers because shapes are binary trees.
     Î± âˆˆ Î³ is the set of variable indices in Î³ whose arity is Î±. -}
  data _âˆˆ_ : Arity â†’ Shape â†’ Set where
    var-here : âˆ€ {Î±} â†’ Î± âˆˆ [ Î± ]
    var-left :  âˆ€ {Î±} {Î³} {Î´} â†’ Î± âˆˆ Î³ â†’ Î± âˆˆ Î³ âŠ• Î´
    var-right : âˆ€ {Î±} {Î³} {Î´} â†’ Î± âˆˆ Î´ â†’ Î± âˆˆ Î³ âŠ• Î´

  -- In several cases we want a map defined on the positions of a shape.
  -- Defining such maps directly is difficult because the relevant recursion
  -- principle is not structural. Instead we use a method suggested by
  -- Guillaume Allais (http://gallais.github.io), which amounts to
  -- working with proof-relevant evidence that the function is defined.

  -- The definition of All, tabulate, lookup and map is taken from
  -- https://github.com/gallais/potpourri/blob/349d2f282a100ea5d82a548455b040939b04e67e/agda/poc/Syntax.agda

  -- A shape has positions, as detailed by _âˆˆ_ above. Often we want a map
  -- that for each positions  Î± âˆˆ Î³ returns an element of P Î±, where P
  -- is a type family indexed by arities. Such a map has the type
  -- âˆ€ Î³ {Î±} â†’ Î± âˆˆ Î³ â†’ P Î±. However, it is often easier to work
  -- with an equivalent representation of such a map, one that closely
  -- follows the structure of shapes.
  --
  -- The following definition accomplishes the idea. Given a type family
  -- P : Arity â†’ Set and a shape Î³, an element of Map P Î³ represents a
  -- map taking each Î± âˆˆ Î³ to an element of P Î±.
  data ShapeMap (P : Arity â†’ Set) : Shape â†’ Set where
    ğŸ˜ : ShapeMap P ğŸ˜
    [_] : âˆ€ {Î±} â†’ P Î± â†’ ShapeMap P [ Î± ]
    _âŠ•_ : âˆ€ {Î³ Î´} â†’ ShapeMap P Î³ â†’ ShapeMap P Î´ â†’ ShapeMap P (Î³ âŠ• Î´)

  -- Given a map on positions of a shape, we can produce the corresponding ShapeMap
  tabulate : âˆ€ {Î³ P} â†’ (âˆ€ {Î±} â†’ Î± âˆˆ Î³ â†’ P Î±) â†’ ShapeMap P Î³
  tabulate {ğŸ˜} f = ğŸ˜
  tabulate {[ _ ]} f = [ f var-here ]
  tabulate {_ âŠ• _} f = tabulate (f âˆ˜ var-left) âŠ• tabulate (f âˆ˜ var-right)

  -- Extensionally equal maps give the same ShapeMap's
  tabulate-ext : âˆ€ {P : Arity â†’ Set} {Î³} {f g : âˆ€ {Î±} â†’ Î± âˆˆ Î³ â†’ P Î±} â†’
                 (âˆ€ {Î±} {x : Î± âˆˆ Î³} â†’ f x â‰¡ g x) â†’ tabulate f â‰¡ tabulate g
  tabulate-ext {Î³ = ğŸ˜} Î¾ = refl
  tabulate-ext {Î³ = [ x ]} Î¾ = cong [_] Î¾
  tabulate-ext {Î³ = Î³ âŠ• Î´} Î¾ = congâ‚‚ _âŠ•_ (tabulate-ext Î¾) (tabulate-ext Î¾)

  -- In reverse direction, we can convert a ShapeMap to the map it represents
  infixl 12 _âˆ™_
  _âˆ™_ : âˆ€ {Î³ P} â†’ ShapeMap P Î³ â†’ (âˆ€ {Î±} â†’ Î± âˆˆ Î³ â†’ P Î±)
  [ p ] âˆ™ var-here = p
  (ps âŠ• _) âˆ™ (var-left x) = ps âˆ™ x
  (_ âŠ• qs) âˆ™ (var-right y) = qs âˆ™ y

  -- Tabulation stores values correctly
  tabulate-âˆ™ : âˆ€ {P : Arity â†’ Set} {Î³} (f : (âˆ€ {Î±} â†’ Î± âˆˆ Î³ â†’ P Î±)) {Î±} {x : Î± âˆˆ Î³} â†’ (tabulate f) âˆ™ x â‰¡ f x
  tabulate-âˆ™ f {x = var-here} = refl
  tabulate-âˆ™ f {x = var-left x} = tabulate-âˆ™ (f âˆ˜ var-left)
  tabulate-âˆ™ f {x = var-right y} = tabulate-âˆ™ (f âˆ˜ var-right)

  -- The operation _âˆ™_ preserves equality (every operation does, but we will need this
  -- specific fact later).
  cong-âˆ™ : âˆ€ {Î³ P} {f g : ShapeMap P Î³} {Î±} {x y : Î± âˆˆ Î³} â†’ f â‰¡ g â†’ x â‰¡ y â†’ f âˆ™ x â‰¡ g âˆ™ y
  cong-âˆ™ refl refl = refl

  -- Map the values stored in a ShapeMap
  map : âˆ€ {Î³ P Q} â†’ (âˆ€ {Î±} â†’ P Î± â†’ Q Î±) â†’ ShapeMap P Î³ â†’ ShapeMap Q Î³
  map f ğŸ˜ = ğŸ˜
  map f [ x ] = [ f x ]
  map f (ps âŠ• qs) = map f ps âŠ• map f qs

  -- Mapping g âˆ˜ f is the same as mapping f followed by mapping g
  map-map : âˆ€ {Î³} {P Q R : Arity â†’ Set} (f : âˆ€ {Î±} â†’ P Î± â†’ Q Î±) (g : âˆ€ {Î±} â†’ Q Î± â†’ R Î±) {p : ShapeMap P Î³} â†’
              map (g âˆ˜ f) p â‰¡ map g (map f p)
  map-map f g {p = ğŸ˜} = refl
  map-map f g {p = [ x ]} = refl
  map-map f g {p = pâ‚ âŠ• pâ‚‚} = congâ‚‚ _âŠ•_ (map-map f g) (map-map f g)

  -- If two ShapeMaps store the same values then they are equal
  shape-â‰¡ : âˆ€ {Î³ P} {ps qs : ShapeMap P Î³} â†’ (âˆ€ {Î±} (x : Î± âˆˆ Î³) â†’ ps âˆ™ x â‰¡ qs âˆ™ x)
            â†’ ps â‰¡ qs
  shape-â‰¡ {ps = ğŸ˜} {qs = ğŸ˜} Î¾ = refl
  shape-â‰¡ {ps = [ x ]} {qs = [ y ]} Î¾ = cong [_] (Î¾ var-here)
  shape-â‰¡ {ps = psâ‚ âŠ• psâ‚‚} {qs = qsâ‚ âŠ• qsâ‚‚} Î¾ =
    congâ‚‚ _âŠ•_ (shape-â‰¡ (Î¾ âˆ˜ var-left)) (shape-â‰¡ (Î¾ âˆ˜ var-right))

  -- The interaction of map and tabulate
  map-tabulate : âˆ€ {P Q : Arity â†’ Set} {Î³} {g : âˆ€ {Î±} â†’ P Î± â†’ Q Î±} â†’ {f : (âˆ€ {Î±} â†’ Î± âˆˆ Î³ â†’ P Î±)} â†’
                 map g (tabulate f) â‰¡ tabulate (g âˆ˜ f)
  map-tabulate {Î³ = ğŸ˜} = refl
  map-tabulate {Î³ = [ _ ]} = refl
  map-tabulate {Î³ = _ âŠ• _} = congâ‚‚ _âŠ•_ map-tabulate map-tabulate

  -- the interaction of map and âˆ™
  map-âˆ™ : âˆ€ {Î³ P} {Q : Arity â†’ Set} â†’ {f : âˆ€ {Î±} â†’ P Î± â†’ Q Î±} {ps : ShapeMap P Î³} {Î± : Arity} {x : Î± âˆˆ Î³} â†’ map f ps âˆ™ x  â‰¡ f (ps âˆ™ x)
  map-âˆ™ {ps = [ _ ]} {x = var-here} = refl
  map-âˆ™ {ps = psâ‚ âŠ• psâ‚‚} {x = var-left x} = map-âˆ™ {ps = psâ‚}
  map-âˆ™ {ps = psâ‚ âŠ• psâ‚‚} {x = var-right x} = map-âˆ™ {ps = psâ‚‚}

  {- We now proceed with the definition of expressions. Because everything is a variable, even symbols, there is a
     single expression constructor x ` ts which forms and expression by applying the variable x to arguments ts. -}

  -- The expressions over a given shape
  data Expr : Shape â†’ Set

  -- Auxiliary definition, think of Arg Î³ Î´ as the set of expressions that may refer to free
  -- variables in Î³ and bound variables in Î´. The name arises because Arg Î³ Î´ is also the
  -- set of possible arguments of a variable of arity Î´ in shape Î³.
  Arg : Shape â†’ Arity â†’ Set
  Arg Î³ Î´ = Expr (Î³ âŠ• Î´)

  -- We define renamings and substitutions here so that they can be referred to.

  -- Renaming
  infix 4 _â†’Ê³_

  -- A renaming from Î³ to Î´  maps every position in Î³ to a position in Î´, while preserving
  -- the arity.
  _â†’Ê³_ : Shape â†’ Shape â†’ Set
  Î³ â†’Ê³ Î´ = ShapeMap (_âˆˆ Î´) Î³

  -- A substitution from Î³ to Î´ maps every position in Î³ of arity Î± to an expression
  -- with free variables Î´ and bound variables Î±
  infix 4 _â†’Ë¢_
  _â†’Ë¢_ : Shape â†’ Shape â†’ Set
  Î³ â†’Ë¢ Î´ = ShapeMap (Arg Î´) Î³

  -- Expressions
  infix 9 _`_
  data Expr where
    _`_ : âˆ€ {Î³ Î´} (x : Î´ âˆˆ Î³) â†’ (ts : Î´ â†’Ë¢ Î³) â†’ Expr Î³

  -- A common idiom
  infix 9 _``_

  _``_ : âˆ€ {Î³ Î´} (x : Î´ âˆˆ Î³) â†’ (ts : âˆ€ {a} (z : a âˆˆ Î´) â†’ Arg Î³ a) â†’ Expr Î³
  x `` ts = x ` tabulate ts

  -- Syntactic equality of expressions
  â‰¡-` : âˆ€ {Î±} {Î³} {x y : Î³ âˆˆ arg Î±} {ts us : Î³ â†’Ë¢ arg Î±} â†’
          x â‰¡ y â†’ (âˆ€ {Î±á¶»} (z : Î±á¶» âˆˆ Î³) â†’ ts âˆ™ z â‰¡ us âˆ™ z) â†’ x ` ts â‰¡ y ` us
  â‰¡-` Î¶ Î¾ = congâ‚‚ (_`_) Î¶ (shape-â‰¡ Î¾)
